class FEmbed {
    defaultHeaders := {
        'User-Agent': Http.defaultUserAgent,
    };

    sourcesApiURL := -> baseURL, id {
        return URL.ensureURL('${baseURL}/api/source/${id}');
    }

    parse := -> _url, { locale } {
        url := URL.ensureURL(_url);
        parsedUrl := URL.parse(url);

        id := Regex('/v/([^?#&/.]+)').firstMatch(url)?.group(1);
        if (id == null) return [];

        return AsyncTask.resolve(
            () => Http.fetch(
                method: 'post',
                url: sourcesApiURL(parsedUrl.origin, id),
                headers: Collection.mergeMap(defaultHeaders, {
                    'Referer': url,
                    'X-Requested-With': 'XMLHttpRequest',
                }),
            ),
            onDone: -> resp {
                parsed := Converter.jsonDecode(resp.body);
                if (!parsed['success']) return [];

                return Collection.mapList(
                    parsed['data'],
                    -> i, x {
                        return EpisodeSource(
                            url: URL.ensureURL(x['file']),
                            headers: Collection.mergeMap(defaultHeaders, {
                                'Referer': parsedUrl.origin,
                            }),
                            quality: x['label'],
                            locale: locale,
                        );
                    }
                );
            },
        );
    }
}
