import 'utils.ht';

extractor := MangaExtractor(
    defaultLocale: $.defaultLocale,
    search: -> terms, locale {
        if (_searcher != null {
            return _searcher(terms);
        }

        return AsyncTask.resolve(
            () => _prepareSearcher(),
            onDone: -> resp {
                return _searcher(terms);
            }
        );
    },
    getInfo: -> url, locale {
        slug := $.parseSlug(url);

        return AsyncTask.resolve(
            () => Http.fetch(
                method: 'get',
                url: URL.ensureURL(url),
                headers: $.defaultHeaders,
            ),
            onDone: -> resp {
                document := HtmlElement.parse(resp.body);
                
                chapters := Collection.mapList(
                    Converter.jsonDecode(
                        Regex('vm\\.Chapters = (.*);')
                            .firstMatch(resp.body)
                            .group(1),
                    ),
                    -> i, x {
                        chapter := $.parseChapterId(x['Chapter']);

                        return ChapterInfo(
                            title: x['ChapterName'],
                            url: $.getChapterURL(slug, chapter),
                            chapter: chapter,
                            locale: $.defaultLocale,
                        );
                    }
                );

                title := document.querySelector('.MainContainer .BoxBody .list-group h1').text.trim();
                thumbnail := document.querySelector('.MainContainer .BoxBody .img-fluid').attributes['src'].trim();

                return MangaInfo(
                    title: title,
                    url: URL.ensureURL(url),
                    thumbnail: ImageDescriber(
                        url: $.getMangaThumbnailURL(slug),
                        headers: $.defaultHeaders,
                    ),
                    chapters: chapters,
                    locale: $.defaultLocale,
                    availableLocales: [
                        $.defaultLocale,
                    ]
                );
            }
        );
    },
    getChapter: -> _url, locale {
        url := URL.ensureURL(_url);

        return AsyncTask.resolve(
            () => Http.fetch(
                method: 'get',
                url: URL.ensureURL(url),
                headers: $.defaultHeaders,
            ),
            onDone: -> resp {
                document := HtmlElement.parse(resp.body);

                host := Regex('vm\\.CurPathName = "([^"]+)"')
                    .firstMatch(resp.body)
                    .group(1);

                slug := Regex('vm\\.IndexName = "([^"]+)"')
                    .firstMatch(resp.body)
                    .group(1);
                
                data := Converter.jsonDecode(
                    Regex('vm\\.CurChapter = (.*);')
                        .firstMatch(resp.body)
                        .group(1)
                );

                chapterId := $.parseChapterId(data['Chapter'], false);
                subPath := data['Directory'];
                totalPages := int.parse(data['Page']);

                return Collection.mapList(
                    Collection.rangeList(0, totalPages),
                    -> i, x {
                        pageId := Regex('(\d{3})$')
                            .firstMatch('000${x + 1}')
                            .group(1);

                        return PageInfo(
                            url: $.getImageURL(host, slug, subPath, chapterId, pageId),
                            locale: $.defaultLocale,
                        );
                    }
                );
            }
        );
    },
    getPage: -> url, locale {
        return ImageDescriber(
            url: url,
            headers: $.defaultHeaders,
        );
    },
);

var _searcher;

_prepareSearcher := ->  {
    return AsyncTask.resolve(
        () => Http.fetch(
            method: 'post',
            url: $.getSearchApiURL(),
            headers: $.defaultHeaders,
        ),
        onDone: -> resp {
            parsed := Converter.jsonDecode(resp.body);
            items := Collection.mapList(parsed, -> i, x {
                return {
                    'i': x['i'],
                    's': x['s'],
                    'a': x['a'].join(' | '),
                }
            });

            fuzzy := FuzzySearch(
                items: items,
                keys: [
                    FuzzySearchKey(
                        getter: (x) => x['s'],
                        weight: 2.0,
                    ),
                    FuzzySearchKey(getter: (x) => x['a']),
                    FuzzySearchKey(getter: (x) => x['i']),
                ]
            );

            _searcher = -> terms {
                return Collection.mapList(
                    fuzzy.search(terms, 10),
                    (i, x) => SearchInfo(
                        title: x.item['s'],
                        url: $.getMangaURL(x.item['i']),
                        thumbnail: ImageDescriber(
                            url: $.getMangaThumbnailURL(x.item['i']),
                            headers: $.defaultHeaders,
                        ),
                        locale: $.defaultLocale,
                    ),
                );
            };
        },
    );
}
