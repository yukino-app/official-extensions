import './config.beize' as config;

extractor := {
    defaultLocale: config.defaultLocale,
    search: -> async terms, locale {
        resp := Http.request({
            method: 'get',
            url: config.getSearchApiUrl(terms),
            headers: config.defaultHeaders,
        }).await;
        parsed := Convert.decodeJson(resp.body);

        return Fiber.runConcurrently(
            parsed.data.sublist(0, 10).map(-> x : -> async {
                coverData := x.relationships.find(-> x : x.type == 'cover_art');
                cover := coverData ? _getCoverImageUrl(x.id, coverData.id).await : null;

                return {
                    title: x.attributes.title[locale]
                        ?? x.attributes.title[config.defaultLocale],
                    url: config.getMangaApiUrl(x.id),
                    thumbnail: cover,
                    locale: config.defaultLocale,
                };
            })
        ).await;
    },
    getInfo: -> async url, locale {
        id := config.parseId(url);
        mangaUrl := config.getMangaApiUrl(id);
        resp := Http.request({
            method: 'get',
            url: mangaUrl,
            headers: config.defaultHeaders
        }).await;
        parsed := Convert.decodeJson(resp.body);
        coverData := parsed.data.relationships.find(-> x : x.type == 'cover_art');
        cover := coverData ? _getCoverImageUrl(parsed.data.id, coverData.id).await : null;
        langs := _getAvailableLanguages(id).await;
        chapters := _getChapters(id, locale, 0, []).await;

        return {
            title: parsed.data.attributes.title[locale]
                ?? parsed.data.attributes.title[config.defaultLocale],
            url: mangaUrl,
            thumbnail: cover,
            chapters: chapters,
            locale: locale,
            availableLocales: langs
        };
    },
    getChapter: -> async url, locale {
        id := url.replaceFirst('id:', '');
        resp := Http.request({
            method: 'get',
            url: config.getMangaServerApiUrl(id),
            headers: config.defaultHeaders,
        }).await;
        parsed := Convert.decodeJson(resp.body);
        chapterUrl := config.getChapterApiUrl(parsed.baseUrl, 'data', parsed.chapter.hash);
        
        return parsed.chapter.data.map(-> x : {
            url: config.getPageSourceUrl(chapterUrl, x),
            locale: locale,
        });
    },
    getPage: -> async url, locale : {
        url: url,
        headers: config.defaultHeaders,
    },
};

_chapterLimit := 500;
_getChapters := -> async id, locale, offset, chapters {
    resp := Http.request({
        method: 'get',
        url: config.getMangaFeedApiUrl(id, locale, _chapterLimit, offset),
        headers: config.defaultHeaders,
    }).await;
    parsed := Convert.decodeJson(resp.body);
    parsed.data.forEach(-> x {
        id := x.id;
        title := x.attributes.title;
        chapterNum := x.attributes.chapter;
        if (chapterNum) {
            chapters.add({
                title: (title?.isNotEmpty ?? false) ? title : null,
                url: 'id:' + id,
                chapter: chapterNum,
                volume: x.attributes.volume,
                locale: locale,
            });
        }
    });
    if (parsed.data.isEmpty()) return chapters;
    return _getChapters(id, locale, offset + _chapterLimit, chapters).await;
};

_getCoverImageUrl := -> async manga, cover {
    resp := Http.request({
        method: 'get',
        url: config.getCoverApiUrl(cover),
        headers: config.defaultHeaders,
    }).await;
    parsed := Convert.decodeJson(resp.body);
    return {
        url: config.getCoverUrl(manga, parsed.data.attributes.fileName),
        headers: config.defaultHeaders,
    };
};

_allLanguagesCache := Languages.all();
_getAvailableLanguages := -> async manga {
    resp := Http.request({
        method: 'get',
        url: config.getMangaChapterOverviewUrl(manga, 1, 1, 100),
        headers: config.defaultHeaders,
    }).await;
    parsed := Convert.decodeJson(resp.body);
    langs := [];
    parsed.data.forEach(-> x {
        lang := RegExp.new(r'\w+').firstMatch(x['attributes']['translatedLanguage'])?.group(0);
        if (lang && _allLanguagesCache.contains(lang)) {
            langs.add(lang);
        }
    });
    return langs;
};
