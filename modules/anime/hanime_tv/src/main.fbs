import './config.fbs' as config;

extractor := {
    defaultLocale: config.defaultLocale,
    search: -> terms, locale async {
        resp := await Http.request({
            method: 'post',
            url: config.searchApiUrl,
            headers: Object.apply({
                ['Content-Type']: 'application/json;charset=UTF-8',
            }, config.defaultHeaders),
            body: Convert.encodeJson({
                search_text: terms,
                tags: [],
                tags_mode: 'AND',
                brands: [],
                blacklist: [],
                order_by: 'created_at_unix',
                ordering: 'desc',
                page: 0,
            }),
        });
        parsed := Convert.decodeJson(resp.body);
        hits := Convert.decodeJson(parsed.hits);

        return hits.map(-> x {
            title := x.name;
            censored := x.is_censored;
            
            return {
                title: _makeTitle(title, censored),
                url: config.getAnimeUrl(x.slug),
                thumbnail: {
                    url: x.cover_url,
                    headers: config.defaultHeaders,
                },
                locale: config.defaultLocale,
            };
        });
    },
    getInfo: -> url, locale async {
        url = Url.ensure(url);
        slug := config.parseSlug(url);
        resp := await _getInfo(slug);

        title := resp.hentai_video.name;
        censored := resp.hentai_video.is_censored;
        url := config.getAnimeUrl(slug);

        return {
            title: _makeTitle(title, censored),
            url: url,
            thumbnail: {
                url: resp.hentai_video.cover_url,
                headers: config.defaultHeaders,
            },
            episodes: [{
                episode: '1',
                url: url,
                locale: config.defaultLocale,
            }],
            locale: config.defaultLocale,
            availableLocales: [config.defaultLocale],
        };
    },
    getSources: -> url, locale async {
        url = Url.ensure(url);
        slug := config.parseSlug(url);
        resp := await _getInfo(slug);

        return resp.videos_manifest.servers.map(-> x {
            return x.streams
                .filter(-> x : x.kind == 'hls')
                .map(-> x {print x.slug;return{
                    url: x.url,
                    headers: config.defaultHeaders,
                    quality: RegExp.new(r'\d+$').firstMatch(x.slug)?.group(0) ?? 'unknown',
                    locale: config.defaultLocale,
                };});
        }).flat(1);
    },
};

_infoCache := {};

_getInfo := -> slug async : _infoCache[slug] ?? _fetchInfo(slug);

_fetchInfo := -> slug async {
    url := config.getAnimeApiUrl(slug);
    resp := await Http.request({
        method: 'get',
        url: url,
        headers: config.defaultHeaders,
    });
    parsed := Convert.decodeJson(resp.body);
    _infoCache[slug] = parsed;
    return parsed;
};

_makeTitle := -> title, censored : title + (censored ? ' (Censored)' : '');
