import './config.fbs' as config;

extractor := {
    defaultLocale: config.defaultLocale,
    search: -> terms, locale async {
        resp := await Http.request({
            method: 'get',
            url: config.getSearchUrl(terms),
            headers: Object.apply(Object.clone(config.defaultHeaders), {
                ['x-inertia-partial-data']: 'anime_list',
                ['x-inertia-partial-component']: 'AnimeIndex',
                ['x-requested-with']: 'XMLHttpRequest',
            }),
        });
        parsed := Convert.decodeJson(resp.body);

        return parsed.props.anime_list.data.map(-> x : {
            title: x.title,
            url: config.getAnimeUrl(x.slug),
            thumbnail: {
                url: x.cover,
                headers: config.defaultHeaders,
            },
            locale: config.defaultLocale,
        });
    },
    getInfo: -> url, locale async {
        url = Url.ensure(url);
        resp := await Http.request({
            method: 'get',
            url: url,
            headers: Object.apply(Object.clone(config.defaultHeaders), {
                ['x-inertia-partial-data']: 'anime,episode_list',
                ['x-inertia-partial-component']: 'AnimeDetail',
                ['x-requested-with']: 'XMLHttpRequest',
            }),
        });
        parsed := Convert.decodeJson(resp.body);
        slug := parsed.props.anime.slug;

        episodes := parsed.props.episode_list.data.map(-> x {
            return {
                episode: x.slug,
                url: config.getEpisodeUrl(slug, x.slug),
                locale: config.defaultLocale,
            };
        });

        return {
            title: parsed.props.anime.title,
            url: url,
            thumbnail: {
                url: parsed.props.anime.cover,
                headers: config.defaultHeaders,
            },
            episodes: episodes,
            locale: config.defaultLocale,
            availableLocales: [config.defaultLocale],
        };
    },
    getSources: -> url, locale async {
        url = Url.ensure(url);
        resp := await Http.request({
            method: 'get',
            url: url,
            headers: Object.apply(Object.clone(config.defaultHeaders), {
                ['x-inertia-partial-data']: 'video_list',
                ['x-inertia-partial-component']: 'Episode',
                ['x-requested-with']: 'XMLHttpRequest',
            }),
        });
        parsed := Convert.decodeJson(resp.body);

        return parsed.props.video_list.data.map(-> x {
            return x.mirror.map(-> x : {
                url: x.code.file,
                headers: config.defaultHeaders,
                quality: x.resolution,
                locale: config.defaultLocale,
            });
        }).flat(1);
    },
};
